grammar org.xtext.example.cps.CPS with org.eclipse.xtext.common.Terminals

generate cps "http://www.xtext.org/example/cps/Cps"

// root
CoursePlanningSystem:
    'Program' name=ID '{'
        (courses+=Course)*
        (students+=Student)*
    '}'
;

Course:
    'Course' courseNumber=ID name=STRING '{'
        'credits' credits=INT
        'year' year=INT
        'term' term=Term
        ('prerequisites' prerequisites=Prerequisite)?
        ('corequisites' corequisites=Prerequisite)?
    '}'
;

enum Term:
    FALL='fall' | WINTER='winter' | BOTH='both'
;

// (supports nested AND/OR)
Prerequisite:
    OrPrerequisite
;

OrPrerequisite returns Prerequisite:
    AndPrerequisite ({OrPrerequisite.left=current} 'OR' right=AndPrerequisite)*
;

AndPrerequisite returns Prerequisite:
    PrimaryPrerequisite ({AndPrerequisite.left=current} 'AND' right=PrimaryPrerequisite)*
;

PrimaryPrerequisite returns Prerequisite:
    CourseReference | '(' Prerequisite ')'
;

CourseReference:
    course=[Course|ID]
;

Student:
    'Student' name=ID '{'
        'completed' '{' (completedCourses+=[Course|ID])* '}'
        'maxCreditsPerTerm' maxCreditsPerTerm=INT
        (schedule+=TermAssignment)*
    '}'
;

TermAssignment:
    {TermAssignment}
    'Term' termNumber=INT season=TermSeason '{'
        (assignedCourses+=[Course|ID])*
    '}'
;

enum TermSeason:
    FALL='Fall' | WINTER='Winter'
;
